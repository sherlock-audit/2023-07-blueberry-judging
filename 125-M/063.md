Spare Syrup Dragonfly

medium

# AuraSpell.openPositionFarm will revert in some cases
## Summary

[[_getJoinPoolParamsAndApprove](https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/spell/AuraSpell.sol#L221-L225)](https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/spell/AuraSpell.sol#L221-L225) returns two arrays: `maxAmountsIn` and `amountsIn`. The implementation of `_getJoinPoolParamsAndApprove` will cause their lengths to be different in some cases. However, when calling `IBalancerVault.joinPool`, if `JoinKind` is EXACT_TOKENS_IN_FOR_BPT_OUT, then `maxAmountsIn`/`assets`/`amountsIn`(in [[userData](https://docs.balancer.fi/reference/joins-and-exits/pool-joins.html#userdata)](https://docs.balancer.fi/reference/joins-and-exits/pool-joins.html#userdata)) must have the same length in [[IBalancerVault.JoinPoolRequest](https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/interfaces/balancer/IBalancerVault.sol#L27-L32)](https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/interfaces/balancer/IBalancerVault.sol#L27-L32), otherwise tx will revert inside `IBalancerVault.joinPool`.

## Vulnerability Detail

```solidity
File: blueberry-core\contracts\spell\AuraSpell.sol
296:     function _getJoinPoolParamsAndApprove(
......
304:         uint256 length = tokens.length;
305:->       uint256[] memory maxAmountsIn = new uint256[](length);
306:->       uint256[] memory amountsIn = new uint256[](length);
307:         bool isLPIncluded;
308: 
309:         for (i; i != length; ) {
310:             if (tokens[i] != lpToken) {
311:                 amountsIn[j] = IERC20(tokens[i]).balanceOf(address(this));
312:                 if (amountsIn[j] > 0) {
313:                     _ensureApprove(tokens[i], vault, amountsIn[j]);
314:                 }
315:                 ++j;
316:->           } else isLPIncluded = true;
317: 
318:             maxAmountsIn[i] = IERC20(tokens[i]).balanceOf(address(this));
319: 
320:             unchecked {
321:                 ++i;
322:             }
323:         }
324: 
325:         if (isLPIncluded) {
326:             assembly {
327:->               mstore(amountsIn, sub(mload(amountsIn), 1))
328:             }
329:         }
......
346:     }
```

The intent of the above code is: if `tokens` array contains `lpToken`, then the `amountsIn` array needs to exclude lpToken's amount.

- From L305-306, `maxAmountsIn`/`amountsIn`/`tokens` have same length.
- From L316, If `tokens[i]` is equal to `lpToken`, the amount of `lpToken` will not be assigned to the item of `amountsIn` array, and `isLPIncluded` is set to true.
- From L327, if `isLPIncluded` is true, `amountsIn.length` subtracts by 1. In this case, `amountsIn.length` will not equal to `maxAmountsIn/tokens.length`.

```solidity
File: blueberry-core\contracts\spell\AuraSpell.sol
117:             (
118:->               uint256[] memory maxAmountsIn,
119:->               uint256[] memory amountsIn,
120:                 uint256 poolAmountOut
121:             ) = _getJoinPoolParamsAndApprove(
122:                     address(vault),
123:                     tokens,
124:                     balances,
125:                     lpToken
126:                 );
127: 
128:             if (poolAmountOut != 0) {
129:                 vault.joinPool(
130:                     wAuraPools.getBPTPoolId(lpToken),
131:                     address(this),
132:                     address(this),
133:                     IBalancerVault.JoinPoolRequest({
134:->                       assets: tokens,
135:->                       maxAmountsIn: maxAmountsIn,
136:->                       userData: abi.encode(1, amountsIn, _minimumBPT),
137:                         fromInternalBalance: false
138:                     })
139:                 );
140:             }
```

From [[Balancer's documentation](https://docs.balancer.fi/reference/joins-and-exits/pool-joins.html#userdata)](https://docs.balancer.fi/reference/joins-and-exits/pool-joins.html#userdata), L136 `abi.encode(1, amountsIn, _minimumBPT)` means **Exact Tokens Join**. The code of [[_joinExactTokensInForBPTOut](https://github.com/balancer/balancer-v2-monorepo/blob/master/pkg/pool-weighted/contracts/BaseWeightedPool.sol#L274)](https://github.com/balancer/balancer-v2-monorepo/blob/master/pkg/pool-weighted/contracts/BaseWeightedPool.sol#L274) from balancer's codebase is:

```solidity
function _joinExactTokensInForBPTOut(
        uint256[] memory balances,
        uint256[] memory normalizedWeights,
        uint256[] memory scalingFactors,
        uint256 totalSupply,
        bytes memory userData
    ) private view returns (uint256, uint256[] memory) {
    	//@audit userData is from caller
        (uint256[] memory amountsIn, uint256 minBPTAmountOut) = userData.exactTokensInForBptOut();
        //@aduit it will revert if balances.length != amountsIn.length
->      InputHelpers.ensureInputLengthMatch(balances.length, amountsIn.length);
......
    }
```

By tracing the call stack, `balances` of `_joinExactTokensInForBPTOut` comes from [[_joinOrExit](https://github.com/balancer/balancer-v2-monorepo/blob/master/pkg/vault/contracts/PoolBalances.sol#L123-L124)](https://github.com/balancer/balancer-v2-monorepo/blob/master/pkg/vault/contracts/PoolBalances.sol#L123-L124). In fact, each item in the `balances` array is the amount of each token in the `IBalancerVault.JoinPoolRequest.assets` array in the pool. In other words, `balances.length` is the same as `assets.length`. [[Since maxAmountsIn.leng is equal to assets.length](https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/spell/AuraSpell.sol#L305)](https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/spell/AuraSpell.sol#L305), we can conclude that the length of `assets`/`maxAmountsIn`/`amountsIn` from `IBalancerVault.JoinPoolRequest` must be the same.

## Impact

AuraSpell.openPositionFarm will revert in some cases.

## Code Snippet

https://github.com/sherlock-audit/2023-07-blueberry/blob/main/blueberry-core/contracts/spell/AuraSpell.sol#L117-L141

## Tool used

Manual Review

## Recommendation

```fix
--- a/blueberry-core/contracts/spell/AuraSpell.sol
+++ b/blueberry-core/contracts/spell/AuraSpell.sol
@@ -300,34 +300,23 @@ contract AuraSpell is BasicSpell {
         address lpToken
     ) internal returns (uint256[] memory, uint256[] memory, uint256) {
         uint256 i;
-        uint256 j;
         uint256 length = tokens.length;
         uint256[] memory maxAmountsIn = new uint256[](length);
         uint256[] memory amountsIn = new uint256[](length);
-        bool isLPIncluded;

         for (i; i != length; ) {
             if (tokens[i] != lpToken) {
-                amountsIn[j] = IERC20(tokens[i]).balanceOf(address(this));
-                if (amountsIn[j] > 0) {
-                    _ensureApprove(tokens[i], vault, amountsIn[j]);
+                amountsIn[i] = IERC20(tokens[i]).balanceOf(address(this));
+                if (amountsIn[i] > 0) {
+                    _ensureApprove(tokens[i], vault, amountsIn[i]);
                 }
-                ++j;
-            } else isLPIncluded = true;
-
-            maxAmountsIn[i] = IERC20(tokens[i]).balanceOf(address(this));
-
+                maxAmountsIn[i] = IERC20(tokens[i]).balanceOf(address(this));
+            }
             unchecked {
                 ++i;
             }
         }

-        if (isLPIncluded) {
-            assembly {
-                mstore(amountsIn, sub(mload(amountsIn), 1))
-            }
-        }
-
         uint256 totalLPSupply = IBalancerPool(lpToken).getActualSupply();
         /// compute in reverse order of how Balancer's `joinPool` computes tokenAmountIn
         uint256 poolAmountOut;
```